/*
* @title - DSMath.sol
* @description -
   Copyright (C) 2018  Terry Wilkinson <terry.wilkinson@finterra.org>
   Adapted version of https://github.com/dapphub/ds-math, includes a div floor rather than only rounding

   Permission is hereby granted, free of charge, to any person obtaining a copy of this software and 
   associated documentation files (the "Software"), to deal in the Software without restriction, including 
   without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell 
   copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the 
   following conditions:

   The above copyright notice and this permission notice shall be included in all copies or substantial 
   portions of the Software.

   THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT 
   LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. 
   IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, 
   WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE 
   SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
*/

pragma solidity ^0.4.18;

library DSMathLib {
    /*
    standard uint256 functions
     */

    function dsadd(uint256 x, uint256 y) public view returns (uint256 z) {
        assert((z = x + y) >= x);
    }

    function dssub(uint256 x, uint256 y) public view returns (uint256 z) {
        assert((z = x - y) <= x);
    }

    function dsmul(uint256 x, uint256 y)public view returns (uint256 z) {
        z = x * y;
        assert(x == 0 || z / x == y);
    }

    function dsdiv(uint256 x, uint256 y)public view returns (uint256 z) {
        z = x / y;
    }

    function min(uint256 x, uint256 y)public view returns (uint256 z) {
        return x <= y ? x : y;
    }
    function max(uint256 x, uint256 y)public view returns (uint256 z) {
        return x >= y ? x : y;
    }

    /*
    uint128 functions (h is for half)
     */


    function hadd(uint128 x, uint128 y)public view returns (uint128 z) {
        assert((z = x + y) >= x);
    }

    function hsub(uint128 x, uint128 y)public view returns (uint128 z) {
        assert((z = x - y) <= x);
    }

    function hmul(uint128 x, uint128 y)public view returns (uint128 z) {
        z = x * y;
        assert(x == 0 || z / x == y);
    }

    function hdiv(uint128 x, uint128 y)public view returns (uint128 z) {
        z = x / y;
    }

    function hmin(uint128 x, uint128 y)public view returns (uint128 z) {
        return x <= y ? x : y;
    }
    function hmax(uint128 x, uint128 y)public view returns (uint128 z) {
        return x >= y ? x : y;
    }


    /*
    int256 functions
     */

    function imin(int256 x, int256 y) public view returns (int256 z) {
        return x <= y ? x : y;
    }
    function imax(int256 x, int256 y) public view returns (int256 z) {
        return x >= y ? x : y;
    }

    /*
    WAD math
     */

    uint128 constant WAD = 10 ** 18;

    function wadd(uint128 x, uint128 y) public view returns (uint128) {
        return hadd(x, y);
    }

    function wsub(uint128 x, uint128 y) public view returns (uint128) {
        return hsub(x, y);
    }

    function wmul(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * y + WAD / 2) / WAD);
    }

    function wdiv(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * WAD + y / 2) / y);
    }
    
    // div floored
    function wdiv_floor(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * WAD) / y);
    }

    function wmin(uint128 x, uint128 y) public view returns (uint128) {
        return hmin(x, y);
    }
    function wmax(uint128 x, uint128 y) public view returns (uint128) {
        return hmax(x, y);
    }

    /*
    RAY math
     */

    uint128 constant RAY = 10 ** 27;

    function radd(uint128 x, uint128 y) public view returns (uint128) {
        return hadd(x, y);
    }

    function rsub(uint128 x, uint128 y) public view returns (uint128) {
        return hsub(x, y);
    }

    function rmul(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * y + RAY / 2) / RAY);
    }

    function rdiv(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * RAY + y / 2) / y);
    }

    function rdiv_floor(uint128 x, uint128 y) public view returns (uint128 z) {
        z = cast((uint256(x) * RAY) / y);
    }

    function rpow(uint128 x, uint64 n) public view returns (uint128 z) {
        // This famous algorithm is called "exponentiation by squaring"
        // and calculates x^n with x as fixed-point and n as regular unsigned.
        //
        // It's O(log n), instead of O(n) for naive repeated multiplication.
        //
        // These facts are why it works:
        //
        //  If n is even, then x^n = (x^2)^(n/2).
        //  If n is odd,  then x^n = x * x^(n-1),
        //   and applying the equation for even x gives
        //    x^n = x * (x^2)^((n-1) / 2).
        //
        //  Also, EVM division is flooring and
        //    floor[(n-1) / 2] = floor[n / 2].

        z = n % 2 != 0 ? x : RAY;

        for (n /= 2; n != 0; n /= 2) {
            x = rmul(x, x);

            if (n % 2 != 0) {
                z = rmul(z, x);
            }
        }
    }

    function rmin(uint128 x, uint128 y) public view returns (uint128) {
        return hmin(x, y);
    }
    function rmax(uint128 x, uint128 y) public view returns (uint128) {
        return hmax(x, y);
    }

    function cast(uint256 x) public view returns (uint128 z) {
        assert((z = uint128(x)) == x);
    }

}